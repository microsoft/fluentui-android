/*
 * Copyright (c) Microsoft Corporation. All rights reserved.
 * Licensed under the MIT License.
 */

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId 'com.microsoft.fluentuidemo'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 41
        versionName '0.1.6'
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion composeVersion
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        dogfood {
            keyAlias 'Dogfood'
            keyPassword = project.hasProperty("signingKeyPassword") ? "$signingKeyPassword" : ""
            storeFile file('keystore.jks')
            storePassword = project.hasProperty("signingKeyStorePassword") ? "$signingKeyStorePassword" : ""
        }
    }
    flavorDimensions 'distribution'
    productFlavors {
        development {
            dimension 'distribution'
        }
        dogfood {
            dimension 'distribution'
            applicationIdSuffix '.df'
            signingConfig signingConfigs.dogfood
            buildConfigField "String", "APP_CENTER_SECRET", project.hasProperty("appCenterSecret") ? "\"$appCenterSecret\"" : "\"\""
        }
    }
    testOptions {
        unitTests{
            includeAndroidResources = true
        }
        unitTests.all {
            testLogging {
                events "passed", "failed", "standardError"
                showCauses true
                showExceptions true
                showStackTraces true
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.exifinterface:exifinterface:$exifInterfaceVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation project(':FluentUI')
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'

    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation ("androidx.compose.foundation:foundation-layout:$composeVersion")
    implementation ("androidx.compose.material:material:$composeVersion")
    implementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:$composeVersion")
    debugImplementation("androidx.compose.ui:ui-test-manifest:$composeVersion")

    // App Center
    def appCenterSdkVersion = '1.8.0'
    dogfoodImplementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    dogfoodImplementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    dogfoodImplementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"

    //JUnit4 framework
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"

    //Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-intents:${espressoVersion}"
    androidTestImplementation "androidx.test:runner:${androidxRunner}"
    androidTestImplementation "androidx.test:rules:${androidxRunner}"

    //Robolectric
    testImplementation "org.robolectric:robolectric:${robolectric}"
}
